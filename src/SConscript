import os.path

source_files = Split("""
crt/crt0.s
lib/mos.s
main.c
""")
                     

def additional_prog_files(target, source, env):
    target_name = target[0].name
    target.append(target_name.replace('.ihx', '.lk'))
    target.append(target_name.replace('.ihx', '.map'))

    return target, source


env = Environment(CC='sdcc',
                  CFLAGS='-mez80_z80 -Iinclude/ --reserve-regs-iy --std-c2x --fomit-frame-pointer',
                  LINKFLAGS='--no-std-crt0 -mez80_z80 --code-loc 100',
                  AS='sdasz80',
                  ASFLAGS='-plosgff',
                  OBJSUFFIX='.rel',
                  ENV={'PATH': os.environ['PATH']})
env.Append(PROGEMITTER=additional_prog_files)
env.Tool('compilation_db')
env.CompilationDatabase()

env.Program('main.ihx', source_files)
env.Command('main.lk.fake', 'main.lk', 'sdldz80 -nf $SOURCE')
env.Command('main.bin', 'main.ihx', 'sdobjcopy -I ihex -O binary $SOURCE $TARGET')

env.Depends('main.bin', 'main.lk.fake')

env.Install('../', 'main.bin')
